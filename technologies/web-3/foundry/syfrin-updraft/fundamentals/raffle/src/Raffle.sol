

/* 
Patrick Colins's (Cyfrin Updraft).

Layout for contract:
1. Version
2. Imports
3. Errors
4. Interfaces, Libraries, Contracts
5. Type declarations
6. State variables
7. Events
8. Modifiers
9. Functions

Layout for functions:
1. Constructor
2. Recive function
3. Fallback function
4. External
5. Public
6. Internal
7. Private
8. View & Pure functions
*/


// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19; 

import { VRFConsumerBaseV2Plus } from "@chainlink/contracts/src/v0.8/vrf/dev/VRFConsumerBaseV2Plus.sol";
import { VRFV2PlusClient } from "@chainlink/contracts/src/v0.8/vrf/dev/libraries/VRFV2PlusClient.sol";


/**	import { Script } from "forge-std/Script.sol";
 * @title Raffle
 * @author @kuzminklk
 * @notice This contract is for creating blockchain lottery
 * @dev Implements Chainlink VRFv.2.5
 */
contract Raffle is VRFConsumerBaseV2Plus {

	// Errors
	error Raffle__NotEnoughEthToEnterRaffle();
	error Raffle__TransferFailed();
	error Raffle__RaffleNotOpen();
	error Raffle__UpkeepNotNeeded(uint256 balance, uint256 playersLength, uint256 raffleState);

	// Type Declarations
	enum RaffleState {
		OPEN, // As 0
		CALCULATING // As 1
	}

	// State Variables
	uint16 private constant REQUEST_CONFIRMATION = 3;
	uint16 private constant NUM_WORDS = 1;

	uint256 private immutable i_entranceFee;
	uint256 private immutable i_interval; /// @dev The duration of lottery in seconds
	uint256 private immutable i_subscriptionId;
	bytes32 private immutable i_keyHash;
	uint32 private immutable i_callbackGasLimit;
	
	address payable[] private s_players;
	address private s_recentWinner;
	uint256 private s_lastTimeStamp;
	RaffleState private s_raffleState;

	// Events
	event RaffleEntered(address indexed player);
	event WinnerPicked(address indexed winner);
	event RequestedRaffleWinner(uint256 indexed requestId);


	constructor(uint256 entranceFee, uint256 interval, address vrfCoordinatorContract, uint256 subscriptionId, bytes32 gasLane, uint32 callbackGasLimit) VRFConsumerBaseV2Plus(vrfCoordinatorContract) {
		i_entranceFee = entranceFee;
		i_interval = interval;
		i_keyHash = gasLane;
		i_subscriptionId = subscriptionId;
		i_callbackGasLimit = callbackGasLimit;

		s_lastTimeStamp = block.timestamp;
		s_raffleState = RaffleState.OPEN;
	}


	function enterRaffle() external payable {
		if (msg.value < i_entranceFee) {
			revert Raffle__NotEnoughEthToEnterRaffle();
		}

		if (s_raffleState != RaffleState.OPEN) {
			revert Raffle__RaffleNotOpen();
		}

		s_players.push(payable(msg.sender));
		emit RaffleEntered(msg.sender);
	}


	/**
	 * @dev This is the function that the Chainlink nodes will call to see if the lottery is ready to have a winner picked.
	 * The following should be true:
	 * 1. The time interval has passed between raffle runs.
	 * 2. The lottery is open.
	 * 3. The contract has ETH.
	 * 4. (Implicitly) Subscription has LINK.
	 * @return upkeepNeeded True if it's time to restart lottery.
	 */
	function checkUpkeep(bytes memory /* checkData */) public view returns (bool upkeepNeeded, bytes memory /* performData */	) {
		bool timeHasPassed = (block.timestamp - s_lastTimeStamp) >= i_interval;
		bool isOpen = s_raffleState == RaffleState.OPEN;
		bool hasBalance = address(this).balance > 0;
		bool hasPlayers = s_players.length > 0;
		upkeepNeeded = timeHasPassed && isOpen && hasBalance && hasPlayers;
		return(upkeepNeeded, "");
	}


	function performUpkeep(bytes calldata /* performData */) external {
		(bool upkeepNeeded, ) = checkUpkeep("");
		if (!upkeepNeeded) {
			revert Raffle__UpkeepNotNeeded(address(this).balance, s_players.length, uint256(s_raffleState));
		}

		s_raffleState = RaffleState.CALCULATING;

		uint256 requestId = s_vrfCoordinator.requestRandomWords(VRFV2PlusClient.RandomWordsRequest({
					keyHash: i_keyHash,	
					subId: i_subscriptionId,
					requestConfirmations: REQUEST_CONFIRMATION,
					callbackGasLimit: i_callbackGasLimit,
					numWords: NUM_WORDS,
					extraArgs: VRFV2PlusClient._argsToBytes(VRFV2PlusClient.ExtraArgsV1({nativePayment: false}))
				})
		);

		emit RequestedRaffleWinner(requestId);
	}


	/* 
	Patrick Colins's (Cyfrin Updraft).

	CEI: Checks, Effects, Interactions Pattern
	*/

	function fulfillRandomWords(uint256 requestId, uint256[] calldata randomWords) internal override {
		
		// Checks
		
		// Effects (Internal Contract State Changes)
		uint256 indexOfWinner = randomWords[0] % s_players.length; 
		address payable recentWinner = s_players[indexOfWinner];
		
		s_recentWinner = recentWinner;
		s_players = new address payable[](0);
		s_lastTimeStamp = block.timestamp;
		s_raffleState = RaffleState.OPEN;

		emit WinnerPicked(recentWinner);

		// Interactions (External Contract Interactions)
		(bool success, ) = recentWinner.call{ value: address(this).balance }("");
		if (!success) {
			revert Raffle__TransferFailed();
		}
	}


	// Getters

	function getEnternceFee() external view returns(uint256) {
		return i_entranceFee;
	}

	function getRaffleState() external view returns(RaffleState) {
		return s_raffleState;
	}

	function getPlayer(uint256 indexOfPlayer) external view returns(address) {
		return s_players[indexOfPlayer];
	}

	function getLastTimeStamp() external view returns(uint256) {
		return s_lastTimeStamp;
	}

	function getRecentWinner() external view returns(address) {
		return s_recentWinner;
	}

}

